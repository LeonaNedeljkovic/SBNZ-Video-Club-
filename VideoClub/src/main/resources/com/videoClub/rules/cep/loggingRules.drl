package com.videoClub.rules.cep

import com.videoClub.event.*;
import com.videoClub.model.*;

declare SuspiciousUserEvent
    @role(event)
    user: User
    reason: String
end


rule "More than 3 logging in unsuccessfully in 10 minutes from one user"
	lock-on-active
    when
        $t1: LoggingEvent($userId: user.id, $user : user)
        Number(intValue >= 3) from accumulate(
            $t2: LoggingEvent(
                this != $t1, 
                successful == false,
                user.id == $userId, 
                this meets[10m] $t1
            ),
            count($t2)
        )
        not (SuspiciousUserEvent(user.id == $userId, reason == "Many unsuccessful logging in from same username"))
    then
    	System.out.println("Many unsuccessful logging in from same username");
    	$user.setAllowedToLogIn(false);
        insert(new SuspiciousUserEvent($user, "Many unsuccessful logging in from same username"));
        update($t1);
        
end
 
rule "Less than 3 logging in unsuccessfully in 10 minutes from one user"
	lock-on-active
    when
        $t1: LoggingEvent($userId: user.id, $user : user)
        Number(intValue < 3) from accumulate(
            $t2: LoggingEvent(
                this != $t1, 
                user.id == $userId,
                successful == false, 
                this meets[10m] $t1
            ),
            count($t2)
        )
        $sp: SuspiciousUserEvent(user.id == $userId, reason == "Many unsuccessful logging in from same username")
    then
    	System.out.println("User fine");
    	$user.setAllowedToLogIn(true);
        update($t1);
        delete($sp);
        
end

rule "No previous logging information about user, user is allowed to log in"
	lock-on-active
    when
        $t1: LoggingEvent($userId: user.id, $user : user, successful == true)
        not (SuspiciousUserEvent(user.id == $userId, reason == "Many unsuccessful logging in from same username"))
    then
    	System.out.println("User fine");
    	$user.setAllowedToLogIn(true);
        update($t1);
        
end