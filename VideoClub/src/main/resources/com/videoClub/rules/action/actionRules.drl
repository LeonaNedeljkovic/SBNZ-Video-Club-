//created on: May 18, 2020
package com.videoClub.rules.action

import com.videoClub.model.*;
import com.videoClub.exception.InvalidDiscount;
import com.videoClub.exception.EmptyGenreList;
import com.videoClub.service.ActionEventService;
import java.time.LocalDate;

global ActionEventService actionEventService;
global LocalDate now;

rule "New Discount action"
    when
        $d : Discount( $rank : titleRank );
        $user : RegisteredUser( title == $rank, containsDiscount() == false );
    then
    	modify($user) {
    		getAction().add($d), 
    		getNotifications().add(new Notification("New Discount Action for you!",
    			"You are rewarded with a discount of "+$d.getAmount()+
    			"% on special offers! Hurry to take advantage of it as it only lasts until the end of the action event.",
    			false, $user))
    	};
end

rule "Invalid Discount"
    when
        Discount( amount > 75 || amount < 10 || discountOffers.isEmpty() );
    then
    	throw new InvalidDiscount();
end

rule "New FreeMinutes action"
    when
        $fm : FreeMinutes( $rank : titleRank );
        $user : RegisteredUser( title == $rank, containsFreeMinutes() == false );
    then
    	$user.getAction().add($fm);
    	$user.setAvailableMinutes($user.getAvailableMinutes() + $fm.getAmount());
    	$user.getNotifications().add(new Notification("Free Minutes for you!",
    			"You are rewarded with "+$fm.getAmount()+"minutes!",
    			false, $user));
    	update($user);
end

rule "Invalid FreeMinutes"
    when
        FreeMinutes( amount > 1000 || amount < 30 );
    then
    	throw new InvalidDiscount();
end

rule "New FreeContent action"
    when
        fc : FreeContent( $rank : titleRank );
        $user : RegisteredUser( title == $rank, containsFreeContent() == false );
    then
    	modify($user) {getAction().add(fc),
    		getNotifications().add(new Notification("Free Content for you!",
    			"You are rewarded with free films in "+fc.contentToString()+ " genre!"+
    			"Hurry to take advantage as this action only lasts until the end of the event.",
    			false, $user))
    	};
end

rule "Invalid FreeContent"
    when
        FreeContent( freeGenres.isEmpty() );
    then
    	throw new EmptyGenreList();
end

rule "Delete expired action events"
	when
		$ae: ActionEvent(  endDate.isBefore(now), $id: id );
	then
		actionEventService.delete($id);
end



	