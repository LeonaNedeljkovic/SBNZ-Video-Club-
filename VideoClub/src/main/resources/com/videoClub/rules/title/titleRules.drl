//created on: May 20, 2020
package com.videoClub.rules.title

import com.videoClub.model.*;
import com.videoClub.bean.*;
import com.videoClub.model.enumeration.*;
import com.videoClub.exception.*;

global BronzeImmunity bronzeImmunity;
global SilverImmunity silverImmunity;
global GoldImmunity goldImmunity;
global BronzeTitle bronzeTitle;
global SilverTitle silverTitle;
global GoldTitle goldTitle;

rule "Acquire Bronze title rule"
	no-loop
	salience 50
    when
    	$user : RegisteredUser( $id : id, title == Rank.NONE );
        $total : Number( intValue >= bronzeTitle.getAcquirePoints() 
        				&& intValue < silverTitle.getAcquirePoints()) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.BRONZE);
		$user.setAvailableMinutes($user.getAvailableMinutes() + bronzeTitle.getRewardPoints());
		System.out.println($user.getUsername() + " acquired BRONZE title with " + $total + 
		" purchased minutes. Reward minutes: " + bronzeTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
		
end

rule "Acquire Silver title rule"
	no-loop
	salience 50
    when
    	$user : RegisteredUser( $id : id, title == Rank.NONE || title == Rank.BRONZE );
        $total : Number( intValue >= silverTitle.getAcquirePoints() 
        				&& intValue < goldTitle.getAcquirePoints()) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.SILVER);
		$user.setAvailableMinutes($user.getAvailableMinutes() + silverTitle.getRewardPoints());
		System.out.println($user.getUsername() + " acquired SILVER title with " + $total + 
		" purchased minutes. Reward minutes: " + silverTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Acquire Gold title rule"
	no-loop
	salience 50
    when
    	$user : RegisteredUser( $id : id, title != Rank.GOLD );
        $total : Number( intValue >= goldTitle.getAcquirePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.GOLD);
		$user.setAvailableMinutes($user.getAvailableMinutes() + goldTitle.getRewardPoints());
		System.out.println($user.getUsername() + " acquired GOLD title with " + $total + 
		" purchased minutes. Reward minutes: " + goldTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Save Bronze title rule"
	salience 40
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.BRONZE );
        $total : Number( intValue >= bronzeTitle.getSavePoints() 
        				&& intValue < silverTitle.getAcquirePoints()) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setAvailableMinutes($user.getAvailableMinutes() + bronzeTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved BRONZE title with " + $total + 
		" purchased minutes. Reward minutes: " + bronzeTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Save Silver title rule"
	salience 40
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.SILVER );
        $total : Number( intValue >= silverTitle.getSavePoints() 
        				&& intValue < goldTitle.getAcquirePoints()) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setAvailableMinutes($user.getAvailableMinutes() + silverTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved SILVER title with " + $total + 
		" purchased minutes. Reward minutes: " + silverTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Save Gold title rule"
	salience 40
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.GOLD );
        $total : Number( intValue >= goldTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setAvailableMinutes($user.getAvailableMinutes() + goldTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved GOLD title with " + $total + 
		" purchased minutes. Reward minutes: " + goldTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Acquire Bronze immunity rule"
	salience 30
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.BRONZE, 
    		immunityPoints >= bronzeImmunity.getAcquirePoints() );
        $total : Number( intValue < bronzeTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
    	$user.setImmunity(Rank.BRONZE);
    	$user.setImmunityPoints($user.getImmunityPoints() - bronzeImmunity.getAcquirePoints());
		System.out.println($user.getUsername() + " acquired BRONZE immunity. " + 
		"Immunity points left: " + $user.getImmunityPoints());
		update($user);
end

rule "Acquire Silver immunity rule"
	salience 30
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.SILVER, 
    		immunityPoints >= silverImmunity.getAcquirePoints() );
        $total : Number( intValue < silverTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
    	$user.setImmunity(Rank.SILVER);
    	$user.setImmunityPoints($user.getImmunityPoints() - silverImmunity.getAcquirePoints());
		System.out.println($user.getUsername() + " acquired SILVER immunity. " + 
		"Immunity points left: " + $user.getImmunityPoints());
		update($user);
end

rule "Acquire Gold immunity rule"
	salience 30
	no-loop
    when
    	$user : RegisteredUser( $id : id, title == Rank.GOLD, 
    		immunityPoints >= goldImmunity.getAcquirePoints() );
        $total : Number( intValue < goldTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
    	$user.setImmunity(Rank.GOLD);
    	$user.setImmunityPoints($user.getImmunityPoints() - goldImmunity.getAcquirePoints());
		System.out.println($user.getUsername() + " acquired GOLD immunity. " + 
		"Immunity points left: " + $user.getImmunityPoints());
		update($user);
end

rule "Save Bronze title with immunity rule"
	salience 20
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.BRONZE );
    then
    	$user.setAvailableMinutes($user.getAvailableMinutes() + bronzeTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved BRONZE title with immunity. " + 
		"Reward minutes: " + bronzeTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Save Silver title with immunity rule"
	salience 20
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.SILVER );
    then
    	$user.setAvailableMinutes($user.getAvailableMinutes() + silverTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved SILVER title with immunity. " + 
		"Reward minutes: " + silverTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Save Gold title with immunity rule"
	salience 20
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.GOLD );
    then
    	$user.setAvailableMinutes($user.getAvailableMinutes() + goldTitle.getRewardPoints());
		System.out.println($user.getUsername() + " saved GOLD title with immunity. " + 
		"Reward minutes: " + goldTitle.getRewardPoints() +
		", Total minutes: " + $user.getAvailableMinutes());
end

rule "Lose Bronze title rule"
	salience 10
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.NONE, title == Rank.BRONZE );
        $total : Number( intValue < bronzeTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.NONE);
		System.out.println($user.getUsername()+" lost BRONZE title. Current title: "+$user.getTitle());
end

rule "Lose Silver title rule"
	salience 10
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.NONE, title == Rank.SILVER );
        $total : Number( intValue < silverTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.BRONZE);
		$user.setAvailableMinutes($user.getAvailableMinutes() + bronzeTitle.getRewardPoints());
		System.out.println($user.getUsername()+" lost SILVER title. Current title: "+$user.getTitle());
end

rule "Lose Gold title rule"
	salience 10
    when
    	$user : RegisteredUser( $id : id, immunity == Rank.NONE, title == Rank.GOLD );
        $total : Number( intValue < goldTitle.getSavePoints() ) from accumulate(
         	Purchase( $minutes : purchasedMinutes, user.getId() == $id ),
         	sum( $minutes ) );
    then
		$user.setTitle(Rank.SILVER);
		$user.setAvailableMinutes($user.getAvailableMinutes() + silverTitle.getRewardPoints());
		System.out.println($user.getUsername()+" lost GOLD title. Current title: "+$user.getTitle());
end