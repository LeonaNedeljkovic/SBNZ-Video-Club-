//created on: May 18, 2020
package com.videoClub.rules.entityDefiner

import com.videoClub.model.drl.Title;
import com.videoClub.model.drl.Immunity;
import com.videoClub.model.enumeration.*;
import com.videoClub.exception.*;

global Immunity bronzeImmunity;
global Immunity silverImmunity;
global Immunity goldImmunity;
global Title bronzeTitle;
global Title silverTitle;
global Title goldTitle;

rule "Invalid title rule"
    when
		invalidAcquiredBronzeTitle() or 
		invalidAcquiredSilverTitle() or
		invalidAcquiredGoldTitle() or
		invalidBronzeTitleSave() or 
		invalidSilverTitleSave() or 
		invalidGoldTitleSave() or
		invalidBronzeTitleReward() or
		invalidSilverTitleReward() or
		invalidGoldTitleReward();
    then
    	throw new InvalidTitle();
end

rule "Invalid immunity"
    when
        invalidBronzeImmunity() or
        invalidSilverImmunity() or
        invalidGoldImmunity();
    then
        throw new InvalidImmunity();
end



query invalidAcquiredBronzeTitle()
	Title( titleRank == Rank.BRONZE && (AcquirePoints < 30
        	|| AcquirePoints >= silverTitle.getAcquirePoints()
        	|| AcquirePoints <= SavePoints) );
end

query invalidAcquiredSilverTitle()
	Title( titleRank == Rank.SILVER && (AcquirePoints >= goldTitle.getAcquirePoints()
        	|| AcquirePoints <= bronzeTitle.getAcquirePoints()
        	|| AcquirePoints <= SavePoints) );
end

query invalidAcquiredGoldTitle()
	Title( titleRank == Rank.GOLD && (AcquirePoints > 10000
        	|| AcquirePoints <= silverTitle.getAcquirePoints()
        	|| AcquirePoints <= SavePoints) );
end

query invalidBronzeTitleSave()
	Title( titleRank == Rank.BRONZE && (SavePoints >= silverTitle.getSavePoints()
        	|| SavePoints >= AcquirePoints) );
end

query invalidSilverTitleSave()
	Title( titleRank == Rank.SILVER && (SavePoints >= goldTitle.getSavePoints()
        	|| SavePoints <= bronzeTitle.getSavePoints()
        	|| SavePoints >= AcquirePoints) );
end

query invalidGoldTitleSave()
	Title( titleRank == Rank.GOLD && (SavePoints > 5000
        	|| SavePoints <= silverTitle.getSavePoints()
        	|| SavePoints >= AcquirePoints) );
end

query invalidBronzeTitleReward()
	Title( titleRank == Rank.BRONZE && (RewardPoints >= silverTitle.getRewardPoints()
        	|| RewardPoints > 600 || RewardPoints < 30) );
end

query invalidSilverTitleReward()
	Title( titleRank == Rank.SILVER && (RewardPoints >= goldTitle.getRewardPoints()
        	|| RewardPoints <= bronzeTitle.getRewardPoints()
        	|| RewardPoints > 600 || RewardPoints < 30) );
end

query invalidGoldTitleReward()
	Title( titleRank == Rank.GOLD && (RewardPoints <= silverTitle.getRewardPoints()
        	|| RewardPoints > 600 || RewardPoints < 30) );
end

query invalidBronzeImmunity()
	Immunity( immunityRank == Rank.BRONZE && (AcquirePoints < 5
        	|| AcquirePoints >= silverImmunity.getAcquirePoints()) );
end

query invalidSilverImmunity()
	Immunity( immunityRank == Rank.SILVER && (AcquirePoints <= bronzeImmunity.getAcquirePoints()
        	|| AcquirePoints >= goldImmunity.getAcquirePoints()) );
end

query invalidGoldImmunity()
	Immunity( immunityRank == Rank.GOLD && (AcquirePoints > 1000
        	|| AcquirePoints <= silverImmunity.getAcquirePoints()));
end
